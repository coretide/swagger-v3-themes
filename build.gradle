plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser' version '1.18.0'
}

group = 'dev.coretide.theme'

static def extractVersionFromGitTag() {
    def tagRef = System.getenv('CI_COMMIT_TAG')
    if (tagRef?.startsWith("v")) {
        return tagRef.replace("v", "")
    }
    tagRef = System.getenv('GITHUB_REF')
    if (tagRef?.startsWith("refs/tags/v")) {
        return tagRef.replace("refs/tags/v", "")
    }

    def gitDir = new File('.git')
    if (!gitDir.exists()) {
        println "Warning: Not in a git repository (.git directory not found)"
        return "0.0.1-SNAPSHOT"
    }

    try {
        def gitTagProcess = ['git', 'describe', '--tags', '--exact-match', 'HEAD'].execute()
        gitTagProcess.waitFor()
        if (gitTagProcess.exitValue() == 0) {
            def gitTag = gitTagProcess.in.text.trim()
            if (gitTag.startsWith("v")) {
                return gitTag.replace("v", "")
            }
            return gitTag
        }

        gitTagProcess = ['git', 'describe', '--tags', '--abbrev=0'].execute()
        gitTagProcess.waitFor()
        if (gitTagProcess.exitValue() == 0) {
            def gitTag = gitTagProcess.in.text.trim()
            if (gitTag.startsWith("v")) {
                def version = gitTag.replace("v", "")
                return version + "-SNAPSHOT"
            }
            return gitTag + "-SNAPSHOT"
        }
    } catch (Exception e) {
        println "Warning: Could not extract version from Git tag: ${e.message}"
    }
    return "0.0.1-SNAPSHOT"
}

version = extractVersionFromGitTag()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    mavenLocal()
    gradlePluginPortal()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        create('maven', MavenPublication) {
            from components.java as SoftwareComponent

            pom {
                name = 'Swagger V3 Themes'
                description = 'A comprehensive theming library for Swagger UI v3+ with 14 beautiful themes and an intuitive theme selector'
                url = 'https://github.com/coretide/swagger-v3-themes'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'coretide'
                        name = 'Kushal Patel'
                        email = 'contact@coretide.dev'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/coretide/swagger-v3-themes.git'
                    developerConnection = 'scm:git:ssh://github.com/coretide/swagger-v3-themes.git'
                    url = 'https://github.com/coretide/swagger-v3-themes'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'staging'
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("jreleaserDeploy") || gradle.taskGraph.hasTask("publishToSonatype") }

    // Use in-memory keys in CI to avoid passphrase prompts
    if (System.getenv('CI') == 'true') {
        def signingKey = System.getenv('GPG_PRIVATE_KEY') ?: System.getenv('JRELEASER_GPG_SECRET_KEY')
        def signingPassword = System.getenv('GPG_PASSPHRASE') ?: System.getenv('JRELEASER_GPG_PASSPHRASE')

        if (signingKey && signingPassword) {
            useInMemoryPgpKeys(signingKey, signingPassword)
        } else {
            useGpgCmd()
        }
    } else {
        useGpgCmd()
    }
    sign publishing.publications.maven
}

tasks.named('jar') {
    archiveFileName.set("${rootProject.name}-${version}.jar")
}

jreleaser {
    project(({
        name = 'swagger-v3-themes'
        description = 'A comprehensive theming library for Swagger UI v3+ with 14 beautiful themes and an intuitive theme selector'
        authors = ['Kushal Patel']
        license = 'MIT'
        copyright = '2025 Kushal Patel'

        links(({
            homepage = 'https://github.com/coretide/swagger-v3-themes'
            bugTracker = 'https://github.com/coretide/swagger-v3-themes/issues'
        } as Closure<Void>))

        inceptionYear = '2025'
        vendor = 'Kushal Patel'
    } as Closure<Void>))

    signing(({
        active = 'RELEASE'
        armored = true
    } as Closure<Void>))

    deploy {
        maven {
            mavenCentral(({
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                    connectTimeout = 60
                    readTimeout = 600
                    retryDelay = 10
                    maxRetries = 5
                }
            } as Closure<Void>))
        }
    }

    release {
        github(({
            enabled = false
        } as Closure<Void>))
    }
}

tasks.register('createStagingDir') {
    doLast {
        file('build/staging-deploy').mkdirs()
    }
}

tasks.named('jreleaserDeploy') {
    dependsOn 'createStagingDir'
}
